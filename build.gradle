// MPS plugin
buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.4.+'
    }
}

plugins {
    id 'base'
    id 'maven-publish'
}

// Versions (to update: mpsVersion with the target MPS version and ecmascript4mpsVersion with the commit number or tag available on https://jitpack.io/#mar9000/ecmascript4mps)
ext.mpsVersion = project.hasProperty("mpsVersion") ? project.property('mpsVersion') : '2021.2.4'
ext.iets3Version = project.hasProperty("iets3Version") ? project.property('iets3Version') : "2021.2.+"
ext.ecmascript4mpsVersion = project.hasProperty("ecmascript4mpsVersion") ? project.property('ecmascript4mpsVersion') : "2021.2"

configurations {
    mps
	mpsArtifacts
    junitAnt
}

dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
	mpsArtifacts "org.iets3:opensource:$iets3Version"
	mpsArtifacts "com.github.mar9000:ecmascript4mps:$ecmascript4mpsVersion"
    junitAnt 'org.apache.ant:ant-junit:1.10.9'
}

repositories {
    mavenLocal()
	maven { url 'https://artifacts.itemis.cloud/repository/maven-mps/' }
    maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    mavenCentral()
    maven { url 'https://jitpack.io' }
}


// Make BuildLanguages and GenerateLibrariesXml available
import de.itemis.mps.gradle.*


// Location of the dependencies
ext.artifactsDir = file("$rootDir/artifacts")

// Optional properties
def mpsDir = project.hasProperty("mpsDir") ? project.property('mpsDir') : "$buildDir/mps"

// ==== resolving java 11 ====
def jdkHome


// Sdk specified
if (project.hasProperty("jdk")) {
    // No further check
    jdkHome = project.property('jdk')
}

// Current java version is 11
else if (JavaVersion.current() == JavaVersion.VERSION_11) {
    // Perfect then -> use it
    jdkHome = System.getProperty('java.home')
}

// Neither specified, but MPS installation provided with a jbr folder !
else if (project.hasProperty("mpsDir") && file("$mpsDir/jbr").exists()) {
    // That was close
    jdkHome = "$mpsDir/jbr"
}

else {
    // No way to get it working that way, in previous releases it was possible to download JDK from bintray
    // but since it moved to teamcity, it requires authentication
    throw new GradleException("unable to locate JDK11 environment, please specify path to one using -Djdk")
}

// Default arguments for ant scripts
def buildScriptClasspath = project.configurations.junitAnt.fileCollection { true } +
	project.files("$jdkHome/lib/tools.jar")

// Download MPS
task resolveMps(type: Copy) {
    dependsOn configurations.mps
    from {
        if (!project.hasProperty("mpsDir")) {
            configurations.mps.resolve().collect { zipTree(it) }
        }
    }
    into mpsDir
}

// Project
version = "$mpsVersion.0"

// Args to the builds scripts, contains both home and artifacts directory
def defaultScriptArgs = [
	'mps_home'                          : mpsDir,

    // Projects folders
	'iets3.github.opensource.genjs.home': rootDir,

    // Generated artifacts
	'iets3.github.opensource.artifacts' : "$artifactsDir/org.iets3.opensource",
    'mbeddr.artifacts.platform'         : "$artifactsDir/com.mbeddr.platform",
	'ecmascript4mps.artifacts'  		: "$artifactsDir/ecmascript4mps",

    // Misc
	'build.dir'                         : rootDir,
	'version'                           : version
]

// Declare them as global variable for the MPS plugin (see https://github.com/mbeddr/mps-gradle-plugin)
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = defaultScriptArgs.collect { "-D$it.key=$it.value".toString() }
ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = buildScriptClasspath
ext["itemis.mps.gradle.ant.defaultJavaExecutable"] = file("$jdkHome/bin/java")

task resolveArtifacts(type: Copy) {
    from {
        configurations.mpsArtifacts.resolve().collect { zipTree(it) }
    }
    into artifactsDir
}

task buildLanguages(type: BuildLanguages, dependsOn: [resolveArtifacts]) {
    if (!project.hasProperty("mpsDir")) dependsOn "resolveMps"

	script "$rootDir/build.xml"
}

task generateLibrariesXml(type: GenerateLibrariesXml) {
    description "Read project libraries from projectlibraries.properties and generate libraries.xml in .mps directory. Libraries are loaded in mps during start."
    defaults rootProject.file('projectlibraries.properties')
	overrides rootProject.file('projectlibraries.overrides.properties')
    destination file('.mps/libraries.xml')
}

task packageLanguages(type: Zip, dependsOn: buildLanguages) {
    description "Package genjs into the zip file"
    archiveBaseName = 'org.iets3.opensource.genjs'
    from artifactsDir
    include 'org.iets3.opensource.genjs/**'
}

publishing {
    publications {
        genJsWithEcmascript(MavenPublication) {
            groupId 'org.iets3.opensource'
            artifactId 'genjs'
            artifact packageLanguages
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.mpsArtifacts.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                }
                configurations.mps.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                    dependencyNode.appendNode('scope', 'provided')
                }
            }
        }
    }
}

defaultTasks 'buildLanguages'
